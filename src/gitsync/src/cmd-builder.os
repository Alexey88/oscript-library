//////////////////////////////////////////////////////////////////////////
// Работа с командными файлами

#Использовать tempfiles

Перем мЗаписьТекста;
Перем мПуть;

Функция Открыть(Знач Путь = "") Экспорт
	
	Если ПустаяСтрока(Путь) Тогда
		мПуть = ВременныеФайлы.НовоеИмяФайла(".bat");
	Иначе
		мПуть = Путь;
	КонецЕсли;
	
	мЗаписьТекста = Новый ЗаписьТекста(мПуть, "cp866");

	Добавить("@chcp 866 > nul");

	Возврат мПуть;
	
КонецФункции

Процедура Добавить(Знач Команда) Экспорт
	ПроверитьЧтоФайлОткрыт();
	Сообщить(Команда);
	мЗаписьТекста.ЗаписатьСтроку(Команда);
КонецПроцедуры

Функция Выполнить(Знач ТекущийКаталог = "") Экспорт
	
	Закрыть();
	
	ПутьПакетногоФайла = мПуть;
	
	СтрокаЗапуска = "cmd.exe /E:ON /A /C """ + ПутьПакетногоФайла + """";
	
	КодВозврата = "";
	Сообщить(СтрокаЗапуска);
	
	Процесс = СоздатьПроцесс(СтрокаЗапуска, ТекущийКаталог, Истина);
	Процесс.Запустить();

	Пока НЕ Процесс.Завершен Цикл
		Пока Процесс.ПотокВывода.ЕстьДанные Цикл
			СтрокаВывода = Процесс.ПотокВывода.ПрочитатьСтроку();
			Сообщить(СтрокаВывода);
		КонецЦикла;
	КонецЦикла;

	КодВозврата = Процесс.КодВозврата;
	
	Возврат КодВозврата;

КонецФункции

Функция Закрыть() Экспорт
	
	Если мЗаписьТекста <> Неопределено Тогда
		мЗаписьТекста.Закрыть();
		мЗаписьТекста = Неопределено;
	КонецЕсли;
	
	Возврат мПуть;
	
КонецФункции

Процедура ПроверитьЧтоФайлОткрыт()
	Если мЗаписьТекста = Неопределено Тогда
		Открыть();
	КонецЕсли;
КонецПроцедуры
